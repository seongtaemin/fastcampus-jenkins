pipeline {
    agent any

    environment {
        // Harbor 설정
        HARBOR_URL = 'harbor.kolon.local'
        HARBOR_PROJECT = 'library'
    }

    stages {
        stage('Initialize & Scan Images') {
            steps {
                script {
                    def userInput = input(
                        message: 'Enter Source Repository to Scan', 
                        parameters: [
                            string(defaultValue: 'https://github.com/seongtaemin/.git', description: 'GitHub Repository URL', name: 'SOURCE_REPO_URL')
                        ]
                    )
                    env.SOURCE_REPO_URL = userInput
                    
                    echo "Cloning ${env.SOURCE_REPO_URL} to scan for images..."
                    sh "rm -rf target-repo && git clone ${env.SOURCE_REPO_URL} target-repo"
                    
                    // target-repo 내의 모든 파일에서 image: 패턴 추출
                    def imageLines = sh(
                        script: '''
                            grep -r "image:" target-repo | grep -v ".git" | while read -r line; do
                                imageValue=$(echo "$line" | sed 's/.*image:[[:space:]]*//' | sed 's/#.*//' | tr -d ' "'"'"'[:space:]')
                                
                                if [ ! -z "$imageValue" ] && echo "$imageValue" | grep -q ':'; then
                                    imageFull=$(echo "$imageValue" | rev | cut -d'/' -f1 | rev)
                                    imageName=$(echo "$imageFull" | cut -d':' -f1)
                                    imageTag=$(echo "$imageFull" | cut -d':' -f2)
                                    
                                    echo "${imageValue}|${imageName}|${imageTag}"
                                fi
                            done | sort -u
                        ''',
                        returnStdout: true
                    ).trim()
                    
                    if (!imageLines) {
                        error "검색된 이미지가 없습니다. 레포지토리에 YAML 파일이 있는지 확인하세요."
                    }
                    
                    env.IMAGE_LIST_STR = imageLines.split('\n').collect { it.trim() }.findAll { it }.join(';')
                    
                    echo "========== 발견된 이미지 목록 =========="
                    env.IMAGE_LIST_STR.split(';').each { entry ->
                        def parts = entry.split('\\|')
                        if (parts.length == 3) {
                            echo "  [검색됨] ${parts[0]}  ==>  [대상] ${HARBOR_URL}/${HARBOR_PROJECT}/${parts[1]}:${parts[2]}"
                        }
                    }
                    echo "========================================"
                }
            }
        }

        stage('Mirror to Harbor') {
            steps {
                script {
                    // Harbor 인증 자격 증명 사용
                    withCredentials([usernamePassword(credentialsId: 'harbor-credentials', usernameVariable: 'HARBOR_USER', passwordVariable: 'HARBOR_PASSWORD')]) {
                        sh "docker login ${HARBOR_URL} -u \${HARBOR_USER} -p \${HARBOR_PASSWORD}"
                        
                        env.IMAGE_LIST_STR.split(';').each { entry ->
                            def parts = entry.split('\\|')
                            if (parts.length == 3) {
                                def sourceImage = parts[0]
                                def targetName = parts[1]
                                def targetTag = parts[2]
                                def targetFull = "${HARBOR_URL}/${HARBOR_PROJECT}/${targetName}:${targetTag}"
                                
                                echo ">>> Processing: ${sourceImage}"
                                try {
                                    sh "docker pull ${sourceImage}"
                                    sh "docker tag ${sourceImage} ${targetFull}"
                                    sh "docker push ${targetFull}"
                                    echo ">>> Successfully mirrored to ${targetFull}"
                                } catch (Exception e) {
                                    echo "!!! Failed to process ${sourceImage}: ${e.getMessage()}"
                                }
                            }
                        }
                    }
                }
            }
        }
    }

    post {
        always {
            echo "작업이 완료되었습니다."
            script {
                sh "docker logout ${HARBOR_URL}"
            }
        }
    }
}
